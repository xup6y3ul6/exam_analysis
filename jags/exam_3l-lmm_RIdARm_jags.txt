
  model {
    # Priors
    beta ~ dnorm(0, 0.001)
    
    psi_s ~ dt(0, 1/6.25, 3)T(0,) #SD for random effects for subjects
    prec_s <- 1 / pow(psi_s, 2)

    psi_d ~ dt(0, 1/6.25, 3)T(0,) # (hetero) SD for random effects for days
    prec_d <- 1 / pow(psi_d, 2)
   
    sigma_epsilon ~ dt(0, 1/6.25, 3)T(0,) #SD for error
    prec_epsilon <- 1 / pow(sigma_epsilon, 2)
    
    sigma_omega_m ~ dt(0, 1/6.25, 3)T(0,)
    prec_omega_m <- 1 / pow(sigma_omega_m, 2)
    
    phi_m ~ dnorm(0, 4)T(-1,1)  #autocorrelation
  
    # Likelihood
    for (i in 1:N) {
      s[i] ~ dnorm(0, prec_s)

      for (j in 1:D) {
        d[i, j] ~ dnorm(0, prec_d)

        omega[i, j, 1] ~ dnorm(0, prec_omega_m) # AR(1) process for the first moment 
        for (k in 2:M) {
          omega_lat[i, j, k] <- phi_m * omega[i, j, k - 1]
          omega[i, j, k] ~ dnorm(omega_lat[i, j, k], prec_omega_m)
        }

        for (k in 1:M) {
          mu[i, j, k] <- beta + s[i] + d[i, j] + omega[i, j, k]
          y[i, j, k] ~ dnorm(mu[i, j, k], prec_epsilon)
        }
      }
    }
    
    # Derived quantity: relative total variance (R_T)
    tau2_m <- sigma_omega_m^2 / (1 - phi_m^2)
    rel_T <- (psi_s^2 + mean(psi_d^2)) / (psi_s^2 + mean(psi_d^2) + tau2_m + sigma_epsilon^2)
  }

